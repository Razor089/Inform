!==================================================
! Story

Constant Story "Ritorno sulla Terra";
Constant Headline "^^Una avventura testuale scritta e programmata da^
                  Cosimo Davide Viggiano.^
                  mail:     cosimod089@@gmail.com^
                  facebook: facebook.com/cosimo.davide^^";
Release 1; Serial "290421";

Replace MakeMatch;
Replace Identical;
Replace NounDomain;
Replace TryGivenObject;

!==================================================
! Include

Include "Parser";
!--------------------------------------------------
! Verbi personalizzati

Object LibraryMessages
  with before [;
    Jump, Sing: print_ret "In una situazione del genere e' l'ultima cosa a cui pensi.";
    Sleep: print_ret "Ti sei appena risvegliato dal sonno criogenico, sei pieno di energia";
    Pray: print_ret "Hai visto tante cose e Dio non e' una di queste.";
    Smell: print_ret "L'aria sembra pulita fortunatamente i depuratori di ossigeno funzionano ancora.";
    Miscellany:
      if(lm_n == 19){
        if(tuta_spaziale has worn)
          "Con la tuta spaziale indosso sei pronto per affrontare i pericoli dello
           spazio siderale.";
        else
          "Indossi la tuta per il sonno criogenico, e' elastica e comoda ma non adatta
          alla vita di tutti i giorni.";
      }
  ];
!--------------------------------------------------
Include "pname";
Include "VerbLib";
Include "Replace";

!==================================================
! Classi

Class Room
  has light;

Class Prop
  has scenery;

Class Porta
  has scenery static lockable door openable locked;

Class Box
  has scenery container openable lockable;
!==================================================
! Stanze

Room sala_criogenica "Sala Criogenica"
  with description "Il dormitorio della nave, in fondo alla stanza sono allineate le
                    capsule criogeniche. Sulla parete est ci sono gli armadietti dell'
                    equipaggio. A ovest una porta automatica conduce al corridoio centrale
                    della nave.",
       alarm true,
       before [;
         Listen: if (self.alarm == true) {
                  print_ret "Senti l'allarme della nave e una voce computerizzata:
                  'ALLARME! ALLARME! SITUAZIONE...CRITICA!'";
                }
                print_ret "Silenzio assoluto rotto occasionalmente dal suono del metallo della
                          nave che si piega. Non e' per nulla un buon segno...";
         Go:
          if(noun == w_to && porta_dormitorio has locked) print_ret "Il portellone della sala criogenica non si apre e' sigillato.";
          if(noun == w_to && porta_dormitorio hasnt locked) { playerTo(corridoio); return true; }
       ],
       w_to porta_dormitorio,
       cant_go "La sala criogenica ha solo una entrata ad ovest della stanza.",
  has;

  Room sala_comando "Sala Comando"
    with description "La sala comando della Nabucodonosor, il cervello della nave. Davanti a te c'e' la grande vetrata che
                      da sullo spazio profondo. Subito prima della vetrata c'e' la consolle con i
                      computer di bordo collegati al mainframe principale. Tutto il resto della sala e' piena di schermi
                      e comandi vari.",
        each_turn [;
          if (player in self) Deadflag = 2;
        ],
    has;

Room corridoio "Corridoio Centrale"
  with description "Il corridoio centrale della nave, puoi immaginarlo come la spine dorsale di
                    tutta la nave. A sud continua verso i motori, a nord il corridoio conduce
                    alla sala di comando, a est c'e' la sala criogenica a ovest c'e' la sala da
                    pranzo",
      e_to porta_dormitorio,
      n_to sala_comando,
      s_to portello_motori,
  has;

Room sala_motori "Sala Motori"
  with description "La sala motori e' l'ambiente piu' grande della nave. Al suo interno ospita il gigantesco
                    motore a gravitazione. La stanza e' rotonda, al centro vi e' il cilindro che va dal pavimento
                    al soffitto nel quale il motore puo' funzionare senza problemi. Tutto intorno vi sono cavi
                    di alimentazione che vanno da e verso il motore e anche degli scomparti piu piccoli collegati
                    anche essi al motore della nave. A nord il portellone da verso il corridoio.",
      n_to corridoio,
      cant_go "La sala non ha altre uscite.",
  has;

!==================================================
! Oggetti

!--------------------------------------------------
! Sala Comando
Prop computer "computer di bordo" sala_comando
  with name 'computer' 'di' 'bordo' 'calcolatore' 'macchina',
      article "il",
      description [;
        print "Il computer di bordo. Da qui si comanda tutta la nave, si possono interrogare tutti i sistemi
              controllare le sezioni della nave. Impostare la rotta e guidare anche manualmente la nave.";
        if(spento)
          print_ret "^^Il computer e' spento.";
        else
          print_ret "^^Sullo schermo ci sono informazioni sullo stato della nave";
      ],
      before [;

      ],
      spento true,
  has;

Prop vetrata "vetrata" sala_comando
  with name 'vetrata' 'vetro',
      article "la",
      description
        "La grande vetrata che va da un lato all'altro della stanza, come un immenso parabrezza permette di vedere
        l'immenso spettacolo che offre lo spazio.^^
        Riempiendo tutta la visuale, oltre la vetrata, si staglia la Terra. Il pianeta blu, culla della razza
        umana, dalla distanza noti che la nave dovrebbe essere in fase di rientro.",
  has female;

!---------------------------------------------------
! Sala Criogenica
Box armadietti "armadietti" sala_criogenica
  with pname 'armadietti',
       description
          "Gli armadietti sembrano quasi uscire dalla parete della nave, sono un tutt'uno
          con il metallo. Ogni armadietto e' uguale eccetto quello del comandante che ha una
          fessura vicino alla serratura.",
       before [;
         Take, PushDir, Push, Pull: print_ret "Impossibile, sono tutt'uno con la parete!";
         Lock: "Questi armadietti non hanno sistemi di blocco.";
       ],
  has pluralname;

Object tuta_spaziale "tuta spaziale" armadietti
  with name 'tuta' 'spaziale',
      article "la tua",
      description
        "La tua tuta spaziale per le operazioni EVA. ^A differenza delle vecchie tute
        , piu' simili ad antichi scafandri, queste sono molto piu' leggere e piccole.",
  has clothing female;

Box armadietto_comandante "armadietto del Comandante" sala_criogenica
  with pname 'armadietto' '.x' 'del' '.x' 'comandante' 'fessura' 'lettore' 'magnetico',
       description [;
        if(self has locked || self hasnt open)
         print_ret "L'armadietto del comandante, prima di ogni partenza ripone qui dentro i suoi
                    effetti personali e tutto cio' che ritiene importante. Al lato la fessura sembra un
                    lettore di schede magnetico.";
         if (foto in armadietto_comandante && quaderno in armadietto_comandante)
          print_ret "All'interno dell'armadietto puoi vedere una foto e un quadernetto.";
         else if (foto in armadietto_comandante)
          print_ret "All'interno dell'armadietto puoi vedere una foto.";
         else if (quaderno in armadietto_comandante)
          print_ret "All'interno dell'armadietto puoi vedere un quadernetto.";
         else
          print_ret "L'armadietto del Comandante e' vuoto.";
       ],
       before [ks;
          Open:
            if(self hasnt locked || badge notin player) {
              return false;
            }
            ks = keep_silent; keep_silent = true;
            <Unlock self badge>; keep_silent = ks;
            return true;
       ],
       after [ks;
          Unlock:
            if (self has locked) return false;
            print "Inserisci il tesserino del Comandante nella fessura e subito un beep
                  e un rumore metallico accompagnano l'apertura dell'armadietto.^^";
            ks = keep_silent; keep_silent = true;
            <Open self>; keep_silent = ks;
            return true;
       ],
       with_key badge,
  has locked;

Object foto "foto" armadietto_comandante
  with name 'foto' 'fotografia' 'pellicola',
      description
        "Ricordi questa foto, l'avete fatta tutti insieme il primo giorno che vi
        siete imbarcati per la prima volta. Tutti felici, sono passati 3 anni
        da quel momento. Il Comandante custodiva gelosamente questa foto.",
has female;

Object quaderno "quadernetto" armadietto_comandante
  with name 'quaderno' 'fogli' 'appunti' 'quadernetto' 'agenda' 'codice' 'codici',
      description
        "La vera chiave universale della nave, il quaderno con all'
        interno tutti i codici di sicurezza per sbloccare qualsiasi cosa.
        Se finisse nelle mani sbagliate sarebbe una tragedia.",
  has;

Object lampeggiante "lampeggiante" sala_criogenica
  with name 'lampeggiante' 'luce' 'lampi',
      description[;
        if(sala_criogenica.alarm == true){
          print_ret "I lampi di luce rossa provengono dal lampeggiante sul soffitto, se si e'
                    attivato allora la situazione e' grave.";
        }
        print_ret "Il lampeggiante sul soffitto, e' spento per fortuna. Si accende solo in
                  casi di estrema emergenza.";
        ],
  has scenery;

Prop capsule "capsule criogeniche" sala_criogenica
  with pname 'capsule' 'criogeniche' 'equipaggio',
      description
        "Le capsule criogeniche che ogni nave possiede. Anche se il viaggio spaziale e'
        diventato molto piu' veloce rispetto al passato, per le lunghe distanze e'
        necessario il loro utilizzo. La tua capsula e' aperta mentre le restanti sono rimaste chiuse.
        La capsula del Comandante e' di un bel rosso acceso.",
  has;

Prop mia_capsule "mia capsula" sala_criogenica
  with pname '.x' 'capsula' '.x' 'criogenica',
      description
        "Qualcosa durante l'avaria ha fatto si' che la tua capsula si aprisse. Al suo
        interno non trovi nulla.",
  has female;

Box capsula_comandante "capsula del Comandante" sala_criogenica
  with pname 'capsula' 'del' 'comandante' 'criogenica',
      description [;
        if (self hasnt open){
          print_ret "La capsula del Comandante della nave, ogni nave ne ha una rossa per identificare
                    subito l'ufficiale in comando, tuttavia questa e' un nuovo modello, sembra
                    ci sia qualcosa sotto.";
        }
        print_ret "All'interno della capsula puoi vedere il Comandante...non sembra dare segni di vita.";
        ],
        before [;
          LookUnder: print_ret "Sotto alla capsula del Comandante puoi vedere un bottone rosso
                                con la scritta Apertura Di Emergenza.";
          Open: if (bottone_capsula.premuto == false) print_ret "Impossibile aprirla con le sole mani ed e' anche molto resistente";
        ],
  has female;

Prop bottone_capsula "pulsante di emergenza della capsula" sala_criogenica
  with name 'bottone' 'emergenza' 'apertura' 'pulsante',
      description
        "Il bottone d'emergenza della capsula.",
        premuto false,
        before [ks;
          Push: self.premuto = true;
            ks = keep_silent; keep_silent = true;
            <Open capsula_comandante>;
            keep_silent = false; ks = keep_silent;
            print_ret "Appena premi il bottone, si sente uno sblocco metallico
                          e il classico sibilo dell'apertura idraulica della capsula.";
        ],
  has;

Object badge "tesserino del Comandante" capsula_comandante
  with name 'tesserino' 'badge' 'scheda' 'tessera',
      article "il",
      description
        "La tessera magnetica del Comandante, con questa puoi accedere alle aree riservate
        della nave.",
        after [;
          Take: print_ret "Con cautela e delicatezza, quasi a non voler svegliare il Comandante,
                          apri le dita della mano, estrendo il suo tesserino.";
        ],
  has;

Prop mano "mano del Comandante" capsula_comandante
  with name 'mano',
      description [;
        if(badge in capsula_comandante) {
          print_ret "Nella mano sembra di vedere una specie di tessera.";
        } else {
          print_ret "La mano fredda dell'ormai defunto Comandante.";
        }
      ],
  has;

Prop comandante "Comandante Morgan" capsula_comandante
  with name 'comandante' 'morgan' 'ufficiale' 'capo',
      description [;
        if (self.esaminato == false) {
          print "Il Comandante Morgan, lo conoscevi da poco ma era il miglior comandante che avessi mai
            avuto. Sempre ordini giusti, sempre che sapeva cosa fare in qualsiasi situazione. Rispettato
            da tutto l'equipaggio. Purtroppo come temevi e' morto e una lacrima ti scende sul viso.
            Ti manchera' la sua compagnia, lui avrebbe saputo subito cosa fare adesso.";
        } else {
          print "Il Comandante Morgan, sempre al suo posto.";
        }
        self.esaminato = true;
        if (badge in capsula_comandante){
          "^^Noti che stringe qualcosa nella mano";
        }
        return true;
        ],
        esaminato false,
  has;

Prop pannello "pannello di controllo" sala_criogenica
  with name 'pannello' 'di' 'controllo' 'schermo' 'display',
      description
        "Il pannello di controllo della porta, per ragioni di sicurezza ogni porta ne ha uno.
        Sfortunatamente solo il comandante puo' accedervi con il suo tesserino.
        Sul display lampeggiante puoi leggere 'BLOCCO DI SICUREZZA ATTIVO'",
        before [ks;
           Open:
             if(self hasnt locked || badge notin player) {
               return false;
             }
             ks = keep_silent; keep_silent = true;
             <Unlock self badge>; keep_silent = ks;
             return true;
        ],
        after [ks;
           Unlock:
             if (self has locked) return false;
             print "Inserisci il tesserino del Comandante nella fessura e il portellino del pannello
                    si apre di scatto rivelando al suo interno un tastierino numerico.^^";
             ks = keep_silent; keep_silent = true;
             <Open self>; keep_silent = ks;
             tastiera.visible = true;
             return true;
        ],
      with_key badge,
  has container openable lockable locked;

Prop tastiera "tastierino numerico" pannello
  with name 'tastiera' 'tasti' 'tastierino' 'numerico' 'tastini' 'pulsanti',
      description
        "Il tastierino numerico che serve per inserire il codice di sblocco della porta.
        Solo il Comandante e' a conoscenza di questi codici.",
      before [;
        Insert, Receive:
          if(quaderno in player && noun == quaderno) {
            <Open porta_dormitorio quaderno>;
            return true;
          }
          print_ret "Le tenti tutte ma non succede nulla.";
      ],
      visible false,
  has container open;

Porta porta_dormitorio "porta della Sala Criogenica"
  with pname 'porta' 'automatica' 'scorrevole' 'blindata' 'tagliafuoco' 'portellone' 'cilindri' 'cilindro' 'criogenica',
    description [;
      if(location == corridoio)
        print "Il portellone blindato che conduce alla Sala Criogenica.";
      if(location == sala_criogenica)
        print "Il portellone blindato conduce al corridoio della nave.";
      print "^Vicino al portello puoi vedere un pannello di controllo lampeggiare.";
      if(porta_dormitorio has locked)
        print "^^ I cilindri di sicurezza sono sigillati, la porta e' bloccata.";
      return true;
    ],
    before [ks;
      Open: if(self hasnt locked || quaderno notin player) {
              print_ret "Il portellone e' automatizzato, ma avvicinandoti non si apre,
                        e' bloccato in qualche modo.";
            }
            print    "Inserisci nel tastierino numerico il codice di sblocco che trovi
                      all'interno del quaderno del Comandante.^
                      Subito uno ad uno i cilindri di sicurezza escono fuori sbloccando la porta.^^";
            ks = keep_silent; keep_silent = true;
            <Unlock self quaderno>; keep_silent = ks;
            return true;
      Unlock: if(tastiera.visible == false) {
                print_ret "Non vedi come potresti.";
              }
              return false;
    ],
    after [ks;
      Unlock:
        if(self hasnt locked) return false;
        ks = keep_silent; keep_silent = true;
        <Open self>; keep_silent = ks;
        return true;
    ],
    door_dir [;
      if(location == corridoio) return e_to;
      else if(location == sala_criogenica) return w_to;
    ],
    door_to [;
      if(location == corridoio) return sala_criogenica;
      else if(location == sala_criogenica) return corridoio;
    ],
    found_in sala_criogenica corridoio,
    with_key quaderno,
  has female;
!--------------------------------------------------
! Corridoio

Porta portello_motori "portellone sala motori"
  with description [;
    if(self in corridoio){
      print_ret "Il portellone di metallo che conduce alla sala motori.";
    }
    if(self in sala_motori) {
      print_ret "Il portellone di metallo che conduce al corridoio.";
    }
  ],
      pname '.x' 'portellone' '.x' 'porta' 'motori' '.x' 'automatica',
      when_closed "Ti avvicini ma il portellone automatico non sembra aprirsi.",
      when_open "Il portellone adeso e' sbloccato e si apre normalmente.",
      found_in corridoio sala_motori,
      door_to [;
        if(self in corridoio) return sala_motori;
        if(self in sala_motori) return corridoio;
      ],
      door_dir [;
        if(self in corridoio) return s_to;
        if(self in sala_motori) return n_to;
      ],
  has;

!==================================================
! Routines

[ Initialise;
 Lookmode = 2;
 location = sala_criogenica;
 "Terra - Sistema Solare - Anno 3221^
 L'umanita' ha conquistato lo spazio, colonizzando altri pianeti, i viaggi spaziali
 sono all'ordine del giorno cosi' come le nuove rotte commerciali tra pianeti.
 Il cargo interplanetare a propulsione gravitazionale 'NABUCODONOSOR' si sta avvicinando
 all'orbita terrestre....^^
 Ti risvegli di colpo, un suono assordante rimbomba nell'aria, lampi rossi attraversano
 la stanza. Tenti di muoverti, sembra non succedere nulla poi ti rendi conto che stai
 fluttuando in assenza di gravita'.^
 Neanche il tempo di riprendere i sensi che ti senti cadere di colpo e un dolore lancinante
 alla schiena ti avvisa che la gravita' e' tornata.^
 In testa un unico pensiero: 'Cosa diavolo sta succedendo?'^^";
];

!==================================================
! Lingua

Include "ItalianG";
